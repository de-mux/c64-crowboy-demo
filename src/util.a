; "Crow Boy"
;(c) 2002 by Eric Odland
;
; File : util.a
;
; Contains : Miscellaneous routines
;


; Function Name:        Wait (modified from Madagascar source)
;
; Purpose:              Pause program execution
;
; Registers used:       .A, .X, .Y
;
; How to use:
;       Load .X with desired wait time.
;       Note: Not recommended to be used inside an IRQ interrupt.
;
Wait    subroutine
        ;tax
.l00    lda #12
.l0     pha
        ldy #199
.l1     dey
        bne .l1
        pla
        tay
        dey
        tya
        bne .l0
        dex
        bne .l00
        rts

; Function Name:        ReadJoy (from C64 Programmers Ref Guide p.345 ©1982 CBM)
;
; Purpose:              read joyshtick 2
;
; Registers used:       .A, .X, .Y
;
; How to use:
;       Call this routine.
;       .X and .Y contain X and Y directions (1, 0, or -1)
;       If carry is clear, then fire button
;
ReadJoy subroutine
        LDA JOY2
        LDY #0
        LDX #0
        LSR
        BCS .R0
        DEY
.R0     LSR
        BCS .R1
        INY
.R1     LSR
        BCS .R2
        DEX
.R2     LSR
        BCS .R3
        INX
.R3     LSR
        RTS


; ******************* CopyBlocks *******************
;
; Purpose:              Copy one or more blocks from one area of memory to another.
;
; Input:
;       .A : the MSB of the source address
;       .Y : the MSB of the destination address
;       .X : the number of blocks to copy
;
CopyBlocks      subroutine
        ; (self-modifying code) setup base addresses for source and dest
        sta .src+2
        sty .dst+2

        ldy #0
.byte:
.src    lda $F000,y
.dst    sta $F000,y
        dey
        bne .byte
        inc .src+2
        inc .dst+2
        dex
        bne .byte
        rts

